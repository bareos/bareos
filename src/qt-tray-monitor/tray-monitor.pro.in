######################################################################
#
#  !!!!!!! IMPORTANT !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
#
#  Edit only tray-monitor.pro.in  -- tray-monitor.pro is built by the ./configure program
#
#  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
#
CONFIG += qt
CONFIG -= debug_and_release
CONFIG( debug, debug|release )  {
   CONFIG -= release
} else {
   CONFIG -= debug
   CONFIG += release
}

QMAKE_LIBDIR += ../lib
QMAKE_CXXFLAGS += @JANSSON_INC@
LIBS         += -lbareoscfg -lbareos

bins.path            = /$(DESTDIR)@bindir@
bins.files           = bareos-tray-monitor
confs.path           = /$(DESTDIR)@confdir@
confs.files          = tray-monitor.conf
conf_client.path     = /$(DESTDIR)@confdir@/tray-monitor.d/client/
conf_client.files    = ../defaultconfigs/tray-monitor.d/client/FileDaemon-local.conf
conf_director.path   = /$(DESTDIR)@confdir@/tray-monitor.d/director/
conf_director.files  = ../defaultconfigs/tray-monitor.d/director/Director-local.conf
conf_monitor.path    = /$(DESTDIR)@confdir@/tray-monitor.d/monitor/
conf_monitor.files   = ../defaultconfigs/tray-monitor.d/monitor/bareos-mon.conf
conf_storage.path    = /$(DESTDIR)@confdir@/tray-monitor.d/storage/
conf_storage.files   = ../defaultconfigs/tray-monitor.d/storage/StorageDaemon-local.conf
icons.path           = /$(DESTDIR)@datarootdir@/pixmaps/
icons.files          = bareos-tray-monitor.xpm
desktopentries.path  = /$(DESTDIR)@datarootdir@/applications/
desktopentries.files = bareos-tray-monitor.desktop
autostarts.path      = /$(DESTDIR)/etc/xdg/autostart/
autostarts.files     = bareos-tray-monitor.desktop

TEMPLATE     = app
TARGET       = bareos-tray-monitor
DEPENDPATH  += .
INCLUDEPATH += ../include .. .

LIBTOOL_LINK = @QMAKE_LIBTOOL@ --silent --tag=CXX --mode=link
LIBTOOL_INSTALL = @QMAKE_LIBTOOL@ --silent --mode=install
QMAKE_LINK   = $${LIBTOOL_LINK} $(CXX)
QMAKE_INSTALL_PROGRAM = $${LIBTOOL_INSTALL} install -m @SBINPERM@ -p
QMAKE_CLEAN += .libs/* bareos-tray-monitor release/bareos-tray-monitor

RESOURCES    = main.qrc
MOC_DIR      = moc
OBJECTS_DIR  = obj
UI_DIR       = ui

# Main directory
HEADERS += tray_conf.h tray-monitor.h traymenu.h systemtrayicon.h mainwindow.h authenticate.h monitoritem.h monitoritemthread.h
SOURCES += authenticate.cpp tray_conf.cpp tray-monitor.cpp traymenu.cpp systemtrayicon.cpp mainwindow.cpp monitoritem.cpp monitoritemthread.cpp

FORMS += mainwindow.ui

INSTALLS += bins conf_client conf_director conf_monitor conf_storage icons desktopentries autostarts

QMAKE_EXTRA_TARGETS += depend
