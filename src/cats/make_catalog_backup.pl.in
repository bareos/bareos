#!/usr/bin/env perl
use strict;
# script advanced by Lars Holger Engelhard - DL5RCW

=head1 SCRIPT

  This script dumps your Bareos catalog in ASCII format
  It works for MySQL, SQLite, and PostgreSQL
  The script will check for /etc/bareos/bareos-dir.conf
  If you use the new config structure /etc/bareos/bareos-dir.d/catalog/*
  the script will recognice and search the appropriate file before extracting data
=head1 USAGE

    make_catalog_backup.pl MyCatalog

=head1 LICENSE

   BAREOSÂ® - Backup Archiving REcovery Open Sourced

   Copyright (C) 2000-2010 Free Software Foundation Europe e.V.

   This program is Free Software; you can redistribute it and/or
   modify it under the terms of version three of the GNU Affero General Public
   License as published by the Free Software Foundation plus additions
   that are listed in the file LICENSE.

   This program is distributed in the hope that it will be useful, but
   WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
   Affero General Public License for more details.

   You should have received a copy of the GNU Affero General Public License
   along with this program; if not, write to the Free Software
   Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
   02110-1301, USA.

=cut

my $cat = shift or die "Usage: $0 catalogname";
my $dir_conf='@sbindir@/bareos-dbcheck -B -c @confdir@';
my $wd = "@working_dir@";
my $directory = '/etc/bareos/bareos-dir.d/catalog';

sub dump_sqlite3
{
    my %args = @_;

    exec("echo .dump | sqlite3 '$wd/$args{db_name}.db' > '$wd/$args{db_name}.sql'");
    print "Error while executing sqlite dump $!\n";
    return 1;
}

# TODO: use just ENV and drop the pg_service.conf file
sub dump_pgsql
{
    my %args = @_;
    umask(0077);

    if ($args{db_address}) {
        $ENV{PGHOST}=$args{db_address};
    }
    if ($args{db_socket}) {
        $ENV{PGHOST}=$args{db_socket};
    }
    if ($args{db_port}) {
        $ENV{PGPORT}=$args{db_port};
    }
    if ($args{db_user}) {
        $ENV{PGUSER}=$args{db_user};
    }
    if ($args{db_password}) {
        $ENV{PGPASSWORD}=$args{db_password};
    }
    $ENV{PGDATABASE}=$args{db_name};
    exec("HOME='$wd' pg_dump -c > '$wd/$args{db_name}.sql'");
    print "Error while executing postgres dump $!\n";
    return 1;               # in case of error
}

sub dump_mysql
{
    my %args = @_;
    umask(0077);
    unlink("$wd/.my.cnf");
    open(MY, ">$wd/.my.cnf")
        or die "Can't open $wd/.my.cnf for writing $@";

    $args{db_address} = $args{db_address} || "localhost";
    my $addr = "host=$args{db_address}";
    if ($args{db_socket}) {     # unix socket is fastest than net socket
        $addr = "socket=$args{db_socket}";
    }

    print MY "[client]
$addr
user=$args{db_user}
password=\"$args{db_password}\"
";
    if ($args{db_port}) {
        print MY "port=$args{db_port}\n";
    }

    close(MY);

    exec("HOME='$wd' mysqldump -f --opt $args{db_name} > '$wd/$args{db_name}.sql'");
    print "Error while executing mysql dump $!\n";
    return 1;
}

sub dump_catalog
{
    my %args = @_;
    if ($args{db_type} eq 'SQLite3') {
        $ENV{PATH}="@SQLITE_BINDIR@:$ENV{PATH}";
        dump_sqlite3(%args);
    } elsif ($args{db_type} eq 'PostgreSQL') {
        $ENV{PATH}="@POSTGRESQL_BINDIR@:$ENV{PATH}";
        dump_pgsql(%args);
    } elsif ($args{db_type} eq 'MySQL') {
        $ENV{PATH}="@MYSQL_BINDIR@:$ENV{PATH}";
        dump_mysql(%args);
    } else {
        die "This database type isn't supported";
    }
}

sub ltrim { my $s = shift; $s =~ s/^\s+//;       return $s };
sub rtrim { my $s = shift; $s =~ s/\s+$//;       return $s };
sub  trim { my $s = shift; $s =~ s/^\s+|\s+$//g; return $s };

open(FP, "$dir_conf -C '$cat'|") or die "Can't get catalog information $@";
# catalog=MyCatalog
# db_type=SQLite
# db_name=regress
# db_driver=
# db_user=regress
# db_password=
# db_address=
# db_port=0
# db_socket=
my %cfg;

while(my $l = <FP>)
{
    if ($l =~ /catalog=(.+)/) {
        if (exists $cfg{catalog} and $cfg{catalog} eq $cat) {
            exit dump_catalog(%cfg);
        }
        %cfg = ();              # reset
    }

    if ($l =~ /(\w+)=(.+)/) {
        $cfg{$1}=$2;
    }
}

if (exists $cfg{catalog} and $cfg{catalog} eq $cat) {
    exit dump_catalog(%cfg);
}

my @ar_file_list = ();
opendir (DIR, $directory) or die $!;
while (my $file = readdir(DIR)) {
    if ( $file =~ /(.+)[.]conf$/) {
        push @ar_file_list, $file;
    }
}
closedir(DIR);
#print "@ar_file_list \n";
my $foundFlag = 0;
my $foundFile;
foreach my $curFile (@ar_file_list){
    open(FP2, $directory.'/'.$curFile) or next; # silent fail - maybe no read permission for the single element
    while (my $curLine = <FP2>) {
        $curLine =~ s/\n//g;
        $curLine =~ s/\r//g;
        if (substr($curLine, 0, 1) eq '#') { next; }
        if ($curLine =~ /\s*Name\s*=\s*$cat/) {
	    print "found $cat in $curLine of file $curFile \n";
            $foundFlag = 1;
            $foundFile=$curFile;
        }
    }
    close(FP2);
}
if (($foundFlag > 0) && ($foundFile ne "")) {
    #print "$cat is found in $search!\n";
    $cfg{'catalog'} = trim($cat);

    #open(FP3, $directory.'/'.$cat.'.conf') or die "Can't get catalog information $@";
    open(FP3, $directory.'/'.$foundFile) or die "Can't get catalog information from $foundFile: $@";
    while(my $curLine = <FP3>) {
        $curLine =~ s/\n//g;
        $curLine =~ s/\r//g;
        if ($curLine =~ /(\w+)=(.+)/) {
            $cfg{trim($1)}=trim($2);
        }
        if ($curLine =~ /DB\sDriver/) {
            if ($curLine =~ /(\w+)\s*=\s*(.+)/) {
                if ($curLine =~ /mysql/i) {
                    $cfg{'db_type'}='MySQL';
                }elsif ($curLine =~ /sqlite/i){
                    $cfg{'db_type'}='SQLite3';
                }elsif ($curLine =~ /postgresql/i) {
                    $cfg{'db_type'}='PostgreSQL';
                }else{
                    $cfg{'db_type'}='unknown';
                }
            }
        }
        if ($curLine =~ /DB\sName/) {
            if ($curLine =~/(\w+)\s*=\s*(.+)/) {
                $cfg{'db_name'} = trim($2);
            }
        }
        if ($curLine =~ /DB\sUser/){
            if ($curLine =~/(\w+)\s*=\s*(.+)/) {
                $cfg{'db_user'} = trim($2);
            }
        }
        if ($curLine =~ /DB\sPassword/) {
            if ($curLine =~/(\w+)\s*=\s*(.+)/) {
                $cfg{'db_password'} = trim($2);
            }
        }
        if ($curLine =~ /DB\sAddress/) {
            if ($curLine =~/(\w+)\s*=\s*(.+)/) {
                $cfg{'db_address'} = trim($2);
            }
        }
        if ($curLine =~ /DB\sPort/) {
            if ($curLine =~/(\w+)\s*=\s*(.+)/) {
                $cfg{'db_port'} = trim($2);
            }
        }
        if ($curLine =~ /DB\sSocket/) {
            if ($curLine =~/(\w+)\s*=\s*(.+)/) {
                $cfg{'db_socket'} = trim($2);
            }
        }
    }
    close(FP3);
}

#while (my ($k,$v)=each %cfg){
#    print "\$cfg: $k=>$v\n";
#}

if (exists $cfg{catalog} and $cfg{catalog} eq $cat) {
    exit dump_catalog(%cfg);
}

print "Can't find your catalog ($cat) in director configuration\n";
exit 1;
